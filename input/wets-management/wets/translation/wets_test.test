#!/usr/bin/env tclsh
#*++
# Project:
#   WETS
#
# Module:
#   WETS tests
#*--

package require Tcl 8.6
package require cmdline
package require logger
package require logger::utils
package require logger::appender
package require tcltest
package require mecate
package require miccautil
package require fileutil
package require csv

set optlist {
    {level.arg warn {Log debug level}}
    {nostart {Don't start test harness program automatically}}
    {timeout.arg 5000 {Command timeout in ms}}
    {nocolor {Don't colorize logging output}}
    {showstates {Show state transitions on sequence diagrams}}
}
array set options [::cmdline::getKnownOptions argv $optlist]

::mecate::log::setlevel $options(level)

tcltest::configure {*}$argv

namespace eval ::wets::test {
    set logger [::logger::init wets::test]
    set appenderType console
    if {!$::options(nocolor) && [dict exist [fconfigure stdout] -mode]} {
        set appenderType colorConsole
    }
    ::logger::utils::applyAppender -appender $appenderType -serviceCmd $logger\
            -appenderArgs {-conversionPattern {\[%c\] \[%p\] '%m'}}
    ::logger::import -all -force -namespace log wets::test

    namespace import ::tcltest::*
    namespace import ::ral::*
    namespace import ::ralutil::*

    # Utility proc log a formated event trace
    proc logTrace {level trace} {
        log::$level [::mecate eventTraceFormat $trace]
    }
    proc logInstr {level trace} {
        log::$level [::mecate instrTraceFormat $trace]
    }
    proc logFatal {level trace} {
        log::$level [::mecate fatalTraceFormat $trace]
    }

    proc setupForTestCase {} {
        wetstest eloop halt
        deleteTraces
    }
    proc deleteTraces {} {
        wetstest clearEventTraceCache
        wetstest discardEventTraces
        wetstest clearInstrTraceCache
        wetstest discardInstrTraces
        wetstest clearFatalTraceCache
        wetstest discardFatalTraces
    }
    proc format_missing_report {model_cmd} {
        set active [pipe {
            $model_cmd reportTransitions * |
            relation restrictwith ~\
                {$NewState ne "IG" && $NewState ne "CH"} |
            relation eliminate ~ Domain
        }]
    
        log::debug \n[relformat $active "Active Transitions"]
    
        format_missing_states_report $active missing_states_report.csv
        format_missing_trans_report $active missing_trans_report.csv
    }
    proc format_missing_states_report {active_trans report_file} {
        set missing_states [pipe {
            relation summarizeby $active_trans {Model NewState} ms_rel\
                Executed int {rsum($ms_rel, "TransCount")} |
            relation restrictwith ~ {$Executed == 0} |
            relation rename ~ NewState State |
            relation group ~ Missing State Executed
        }]
    
        set report {}
    
        relation foreach missing $missing_states -ascending Model {
            relation assign $missing
            append report\
                    State,Executed\n\
                    [::ral csv $Missing State 1]\
        }
    
        if {$report eq {}} {
            set report "\n*All state activities executed.*\n"
        }
    
        ::fileutil::writeFile $report_file $report
    
        return
    }
    proc format_missing_trans_report {active_trans report_file} {
        set missing_trans [pipe {
            relation restrictwith $active_trans {$TransCount == 0} |
            relation group ~ Transitions State Event NewState TransCount
        }]
    
        set report {}
    
        relation foreach missing $missing_trans -ascending Model {
            relation assign $missing
            append report\
                    State,Event,NewState,Count\n\
                    [::ral csv $Transitions State 1]\
        }
    
        if {$report eq {}} {
            set report "\n*All state transitions taken.*\n"
        }
    
        ::fileutil::writeFile $report_file $report
    
        return
    }

    proc transit_gate {gate} {
        # A Transit Lane has been assigned and V1 has been requested to open.
        # Move things along with a Valve opened event.
        wetstest signal wets Transit_Lane_Gate $gate Valve_opened
        wetstest eloop toc nowait

        # Indicate that water flow has stopped
        wetstest signal wets Transit_Lane_Gate $gate Flow_zero
        wetstest eloop toc nowait

        # Open the gate
        wetstest signal wets Transit_Lane_Gate $gate Gate_opened
        wetstest eloop toc nowait

        # Move the vessel through the gate
        wetstest signal wets Transit_Lane_Gate $gate Vessel_moved
        wetstest eloop toc nowait

        # Close the gate
        wetstest signal wets Transit_Lane_Gate $gate Gate_closed
        wetstest eloop toc nowait

        # Close the valve
        wetstest signal wets Transit_Lane_Gate $gate Valve_closed
        wetstest eloop toc nowait
    }

    proc transfer_vessel_up {lane} {
        for {set gate 1} {$gate < 6} {incr gate} {
            transit_gate g${gate}_${lane}
        }
    }

    proc transfer_vessel_down {lane} {
        for {set gate 5} {$gate > 0} {incr gate -1} {
            transit_gate g${gate}_${lane}
        }
    }

    # Determine which transit lane was assigned by trapping the "Vessel_assigned" event
    proc adjust_gate {gate} {
        # Move things along with a Valve opened event.
        wetstest signal wets Transit_Lane_Gate $gate Valve_opened
        wetstest eloop toc nowait

        # Indicate that water flow has stopped
        wetstest signal wets Transit_Lane_Gate $gate Flow_zero
        wetstest eloop toc nowait

        # Close the valve
        wetstest signal wets Transit_Lane_Gate $gate Valve_closed
        wetstest eloop toc nowait
    }

    proc get_assigned_transit_lane {} {
        set trace [wetstest waitForEventTrace type transition source Wets.toadscanal event Vessel_assigned]
        return [lindex [split [dict get $trace target] .] 1]
    }

    # The test script starts by creating an object of the "rein" class
    # to interact with the test harness.
    mecate rein create ::wetstest -timeout $::options(timeout)
    wetstest seqDiagConfig -showstates $::options(showstates)

    if {!$::options(nostart)} {
        wetstest start ./wets_test_harness
    }

    # Load up the domain information
    miccautil model create wets_model wets.ral

    # Fatal errors can come at any time, so be prepared.
    wetstest fatalNotify [namespace code {logFatal error}]

    # Form a connection to the test harness.
    wetstest connect

    wetstest traceNotify [namespace code {logTrace info}]
    # wetstest instrNotify [namespace code {logInstr info}]

    try {
        wetstest trace on
        wetstest instr on

        # Patch in the transition counting
        wets_model startMecateTransitionCount wetstest

        log::info "**** Begin Test Set 1 ****"
        setupForTestCase

        # Transfer request going up
        wetstest signal wets Wets toadscanal Transfer_request "VS-9300" 0
        wetstest eloop toc nowait
        # A Transit Lane has been assigned and V1 has been requested to open.
        set transit_lane [get_assigned_transit_lane]
        log::info "First up request assigned to $transit_lane"

        # Move things along to transit the gate
        transfer_vessel_up $transit_lane
        wetstest seqDiagToFile test-1.0-seqdiag.txt
        deleteTraces

        log::info "**** Begin Test Set 2 ****"
        setupForTestCase

        # At this point, we still have one Transit Lane available for up and two for down.
        # Transfer request going up
        wetstest signal wets Wets toadscanal Transfer_request "VS-9301" 0
        wetstest eloop toc nowait
        set transit_lane [get_assigned_transit_lane]
        log::info "Second up request assigned to $transit_lane"

        # Request another up transit
        # This should cause water levels to be adjusted since middle and south
        # are available as "down"
        wetstest signal wets Wets toadscanal Transfer_request "VS-9305" 0
        wetstest eloop toc nowait
        set transit_lane_up2 [get_assigned_transit_lane]
        log::info "Third up request assigned to $transit_lane_up2"

        # Move things along for the first vessel
        transfer_vessel_up $transit_lane
        wetstest seqDiagToFile test-2.0-seqdiag.txt
        deleteTraces

        # Adjust water levels for the second vessel so that it is
        # available as up
        adjust_gate g5_${transit_lane_up2}
        adjust_gate g4_${transit_lane_up2}
        adjust_gate g3_${transit_lane_up2}
        adjust_gate g2_${transit_lane_up2}
        adjust_gate g5_${transit_lane_up2}
        adjust_gate g4_${transit_lane_up2}
        adjust_gate g3_${transit_lane_up2}
        adjust_gate g5_${transit_lane_up2}
        adjust_gate g4_${transit_lane_up2}
        adjust_gate g5_${transit_lane_up2}
        wetstest seqDiagToFile test-2.1-seqdiag.txt
        deleteTraces

        log::info "**** Begin Test Set 3 ****"
        setupForTestCase

        # With water levels adjusted, the vessel can be transferred up
        transfer_vessel_up ${transit_lane_up2}
        wetstest seqDiagToFile test-3.0-seqdiag.txt
        deleteTraces

        log::info "**** Begin Test Set 4 ****"
        setupForTestCase

        # Transfer a vessel down
        # At this time, only middle remains available for a down transfer
        wetstest signal wets Wets toadscanal Transfer_request "VS-9302" 1
        wetstest eloop toc nowait
        set transit_lane [get_assigned_transit_lane]
        log::info "First down request assigned to $transit_lane"

        # Request another down transit
        # This should cause water levels to be adjusted since middle and south
        # are available as "up"
        log::info "[wetstest read wets Transit_Lane north]"
        log::info "[wetstest read wets Transit_Lane middle]"
        log::info "[wetstest read wets Transit_Lane south]"

        wetstest signal wets Wets toadscanal Transfer_request "VS-9307" 1
        wetstest eloop toc nowait
        set transit_lane_down2 [get_assigned_transit_lane]
        log::info "Second down request assigned to $transit_lane_down2"

        transfer_vessel_down $transit_lane

        wetstest seqDiagToFile test-4.0-seqdiag.txt
        deleteTraces

        log::info "**** Begin Test Set 5 ****"
        setupForTestCase

        # Adjust water levels to make the transit lane available for down
        adjust_gate g1_${transit_lane_down2}
        adjust_gate g2_${transit_lane_down2}
        adjust_gate g1_${transit_lane_down2}
        adjust_gate g3_${transit_lane_down2}
        adjust_gate g2_${transit_lane_down2}
        adjust_gate g1_${transit_lane_down2}
        adjust_gate g4_${transit_lane_down2}
        adjust_gate g3_${transit_lane_down2}
        adjust_gate g2_${transit_lane_down2}
        adjust_gate g1_${transit_lane_down2}

        wetstest seqDiagToFile test-5.0-seqdiag.txt
        deleteTraces

        transfer_vessel_down $transit_lane_down2

        wetstest seqDiagToFile test-5.1-seqdiag.txt
        deleteTraces

        wets_model stopMecateTransitionCount wetstest

        format_missing_report wets_model

        deleteTraces
    } finally {
        wetstest destroy
        wets_model destroy
    }

    cleanupTests
}
