metadata
    Title : Transit Lane State Machine Diagram
    Author : Michael M. Lee
    Document ID : mint.wets.td.4
    Creation date : May 23, 2025
    Version : 0.4
    Organization : Model Integration, LLC
    Copyright notice > MIT
    Organization logo > mint
domain Wets Management
class Transit Lane
events
    Vessel assigned
    Make adjustments
    Start transfer
    Move complete
    Continue transfer
    Finished transfer
    Adjust complete
    Continue adjustment
--
state Idle
activity
transitions
    Vessel assigned > Assess Water Level
--
state Assess Water Level ()
activity
    // See if the Assigned Vessel's transfer direction is the same as this
    // Transit Lane's Available transfer direction

    (/R4/R2/Vessel.Transfer direction == Available transfer direction)?

    // Same transfer direction, so no water adjustments to do. Start transfer.

        Start transfer -> me :
    
    // Different transfer directions, so first set up R12 to point to first 
    // Adjustment Step to do the required a raise or lower  ==> Scrall TBD
    //
    // Then go do the work

        Make adjustments -> me

transitions
    Make adjustments > Adjust Water Level
    Start transfer > Request Gate Move
--
state Adjust Water Level ()
activity
    // See whether we need to adjust the water levels up or down.

    (/R4/R2/Vessel.Transfer direction == _up)?
    {
        // Adjust water levels for an up transfer. Algorithm to be designed.
        // Update this attribute.
        Available transfer direction = _up
    } :
    {
        //Adjust water levels for a down transfer. Algorithm to be designed.
        // Update this attribute.
        Available transfer direction = _down
    }
    // Water levels adjusted, start the transfer.
    Start transfer -> me 

transitions
    Start transfer > Request Gate Move
--
state Request Gate Move ()
activity
    // Get the gate needing to do a move

    move gate .= /R4/R10/Transit Lane Gate

    Move vessel -> move gate
    
transitions
    Move complete > Assess Transfer Complete
--
state Request Gate Adjustment ()
activity
    // SCRALL TBD
    // 1. Get the gate needing to do an adjustment 
    // 2. Send Adjust level event to this gate 
    
transitions
    Adjust complete > Assess Adjustment Complete
--
state Assess Adjustment Complete ()
activity
    // SCRALL TBD
    // 1. Get the last Transit Lane Gate adjusted
    // 2. Get the next Transit Lane Gate to be adjusted 
    // 3. If there is a next Transit Lane Gate to be adjusted
    //      Update R12 and send a Continue adjustment event to me
    //    Else
    //      Reset R12 and send a Start transfer event to me
    
transitions
    Continue adjustment > Request Gate Adjustment
    Start transfer > Request Gate Move
--
state Assess Transfer Complete ()
activity
    // Get the last Transit Lane Gate moved past

    last transit lane gate .= /R4/R10/Transit Lane Gate

    // Get the next Transit Lane Gate in the vessel's transfer direction

    /R4/R2/Vessel.Transfer direction == _up?
    // Going up, next gate will bw upstream
        next transit lane gate .= last transit lane gate/OR7/upstream :  
    //Going down, next gate will be downstream
        next transit lane gate .= last transit lane gate/OR7/downstream 

    // Is there a next transit lane gate or are we finished?

    next transit lane gate?  
    // Yes we have one, relate the vessel to this new gate via R10 and continue transfer
    {
        /R4/Assigned Vessel &R10 next transit lane gate

        Continue transfer -> me
    } :
    {
    // No, we don't have one, the last gate move must have completed the vessel's transfer.
    // Report transfer completed to Wets and return to idle state. 

        Transfer completed (free transit lane : me, completed license : /R4/R2/Vessel.License) -> /R1/Wets
        Finished transfer -> me
    }

transitions
    Continue transfer > Request Gate Move
    Finished transfer > Idle
--
